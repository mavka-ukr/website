# Інтернет

```мавка
взяти біб інтернет

інтернет.звʼязатись(
  "127.0.0.1", 
  8080,
  дія(звʼязок, помилка)
    якщо помилка
      друк("помилка звʼязку")
    інакше
      друк("успішно звʼязано")
    кінець
  кінець
)
```

Визначення модуля `інтернет`:

```мавка
структура Звʼязок
  іа текст або юнікод
  порт число
  обробник_даних предмет
  обробник_закінчення предмет
  обробник_відключення предмет
  користувацькі_дані
кінець
```

```мавка
структура Обслуговувач
  іа текст або юнікод
  порт число
  обробник_підключення предмет
  обробник_зупинки предмет
кінець
```

```мавка
дія звʼязатись(іа текст або юнікод, порт число, відклик предмет)
```

```мавка
дія закінчити(звʼязок Звʼязок)
```

```мавка
дія відключити(звʼязок Звʼязок)
```

```мавка
дія обслуговувати(іа текст або юнікод, порт число, відклик предмет)
```

```мавка
дія зупинити(обслуговувач Обслуговувач)
```

## Приклади

<details>
<summary>інтернет обслуговувач</summary>

```мавка
взяти біб інтернет

дія обробити_зупинку_обслуговувача(обслуговувач, помилка)
  якщо помилка
    друк("зупинено з помилкою", помилка)
  інакше
    друк("зупинено")
  кінець
кінець

дія обробити_дані_звʼязку(звʼязок, дані)
  вивести("отримано", юнікод(дані))
  інтернет.надіслати(дані, звʼязок)

  вивести("надіслано", юнікод(дані))
кінець

дія обробити_закінчення_звʼязку(звʼязок, помилка)
  друк("клієнт закінчив повідомлення")
кінець

дія обробити_відключення_звʼязку(звʼязок, помилка)
  якщо помилка
    друк("відключено з помилкою", помилка)
  інакше
    друк("відключено")
  кінець
кінець

дія обробити_підключення_звʼязку_обслуговувача(обслуговувач, звʼязок, помилка)
  якщо помилка
    друк("помилка підключення", помилка)
  інакше
    друк("підключено", звʼязок)

    звʼязок.обробник_даних = обробити_дані_звʼязку
    звʼязок.обробник_закінчення = обробити_закінчення_звʼязку
    звʼязок.обробник_відключення = обробити_відключення_звʼязку
  кінець
кінець

дія обробити_запуск_обслуговувача(обслуговувач, помилка)
  якщо помилка
    друк("помилка запуску", помилка)
  інакше
    обслуговувач.обробник_зупинки = обробити_зупинку_обслуговувача
    обслуговувач.обробник_підключення_звʼязку = обробити_підключення_звʼязку_обслуговувача
    друк("запущено")
  кінець
кінець

інтернет.обслуговувати(
  "0.0.0.0",
  8080,
  обробити_запуск_обслуговувача
)
```

</details>

<details>
<summary>інтернет звʼязок</summary>

```мавка
взяти біб інтернет

дія обробити_дані_звʼязку(звʼязок, дані)
  вивести("отримано", юнікод(дані))
кінець

дія обробити_закінчення_звʼязку(звʼязок, помилка)
  друк("обслуговувач закінчив повідомлення")
кінець

дія обробити_відключення_звʼязку(звʼязок, помилка)
  якщо помилка
    друк("відключено з помилкою", помилка)
  інакше
    друк("відключено")
  кінець
кінець

дія обробити_підключення_звʼязку(звʼязок, помилка)
  якщо помилка
    друк("помилка підключення", помилка)
  інакше
    друк("підключено", звʼязок)

    звʼязок.обробник_даних = обробити_дані_звʼязку
    звʼязок.обробник_закінчення = обробити_закінчення_звʼязку
    звʼязок.обробник_відключення = обробити_відключення_звʼязку

    інтернет.надіслати(ю"привіт\n", звʼязок)
    вивести("надіслано", ю"привіт\n")
  кінець
кінець

інтернет.звʼязатись(
  "127.0.0.1",
  8080,
  обробити_підключення_звʼязку
)
```

</details>